apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}


afterEvaluate { project ->
    publishing {
        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/Skyscanner/backpack-android")
                credentials {
                    username = rootProject.ext.githubUsername.isEmpty() ? System.getenv("GITHUB_ACTOR") : rootProject.ext.githubUsername
                    password = rootProject.ext.githubToken.isEmpty() ? System.getenv("GITHUB_TOKEN") : rootProject.ext.githubToken
                }
            }
        }

        publications {
            maven(MavenPublication) {
                groupId rootProject.ext.group
                artifactId project.artifactId
                version project.version

                from components.release

                pom {
                    name = project.artifactId
                    description = "Backpack is a collection of design resources, reusable components and guidelines for creating Skyscanner's products."
                    url = 'https://github.com/Skyscanner/backpack-android'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'https://github.com/Skyscanner/backpack-android/blob/main/LICENSE.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'skyscanner'
                            name = 'Skyscanner Open Source'
                            email = 'koalasquad@skyscanner.net'
                        }
                    }
                    scm {
                        connection = 'scm:git:git://github.com/Skyscanner/backpack-android.git'
                        developerConnection = 'scm:git:ssh://github.com/Skyscanner/backpack-android.git'
                        url = 'http://github.com/Skyscanner/backpack-android/tree/main'
                    }
                }
            }
        }
    }
}

tasks.register('checkMavenCredentials') {
    doLast {
        def props = ["signing.keyId", "signing.password", "signing.secretKeyRingFile"]
        // Skip GitHub credentials check as they can be provided by GitHub Actions
        if (!System.getenv("GITHUB_ACTIONS") && props.any { rootProject.ext[it]?.isEmpty() }) {
            ant.fail("Missing signing credentials")
        }
    }
}

ext["signing.keyId"] = rootProject.ext["signing.keyId"]
ext["signing.password"] = rootProject.ext["signing.password"]
ext["signing.secretKeyRingFile"] = rootProject.ext["signing.secretKeyRingFile"]

signing {
    sign publishing.publications
}

tasks.withType(Sign) {
    onlyIf { !project.version.endsWith("SNAPSHOT") }
}
