apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'org.jetbrains.dokka-android'
apply plugin: 'com.github.dcendents.android-maven'

apply from: 'gradle-maven-push.gradle'

version = rootProject.ext.version
group = 'com.github.skyscanner.backpack-android'

repositories {
  google()
  mavenCentral()
}
android {
  compileSdkVersion rootProject.ext.compileSdkVersion
  buildToolsVersion rootProject.ext.buildToolsVersion

  defaultConfig {
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.targetSdkVersion
    versionName "1.0"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }
  flavorDimensions "version"
  productFlavors {
    oss {
      dimension "version"
      versionNameSuffix "-oss"
    }
    internal {
      dimension "version"
      versionNameSuffix "-internal"
    }
  }
  defaultPublishConfig "ossRelease"
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

dependencies {
  implementation "androidx.annotation:annotation:$rootProject.ext.androidxAnnotations"
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  implementation "androidx.appcompat:appcompat:$rootProject.ext.androidxAppCompat"
  implementation "com.google.android.material:material:$rootProject.ext.androidMaterial"
  implementation "androidx.constraintlayout:constraintlayout:$rootProject.ext.constraintLayout"
  implementation "androidx.cardview:cardview:$rootProject.ext.cardView"
  implementation "androidx.swiperefreshlayout:swiperefreshlayout:$rootProject.ext.swiperefreshLayout"
  implementation "com.jakewharton.threetenabp:threetenabp:$rootProject.ext.threeTenAbp"
  implementation "com.google.android.gms:play-services-maps:$rootProject.ext.googleMaps"
  androidTestImplementation "androidx.test.ext:junit:$rootProject.ext.junitAndroid"
  androidTestImplementation "androidx.test.espresso:espresso-core:$rootProject.ext.espressoCore"
  androidTestImplementation "androidx.test:rules:$rootProject.ext.testRules"
  androidTestImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:$rootProject.ext.mockitoKotlin"
  androidTestImplementation "org.mockito:mockito-android:$rootProject.ext.mockitoAndroid"
  testImplementation "junit:junit:$rootProject.ext.junit"
}

dokka {
  moduleName = "$rootProject.name"
  outputFormat = 'html'
  outputDirectory = "$rootProject.buildDir/docs"
  reportUndocumented = false

  if (project.hasProperty("dokkaOutput")) {
    outputDirectory = dokkaOutput
  }

  linkMapping {
    dir = "${project.projectDir}/src/main/java"
    url = "https://github.com/Skyscanner/backpack-android/tree/main/${project.name}/src/main/java"
    suffix = "#L"
  }
}
