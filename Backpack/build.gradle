apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.facebook.testing.screenshot'

version = '0.2.0'
group = 'net.skyscanner.backpack'
repositories {
  google()
  mavenCentral()
}
android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    defaultConfig {
        minSdkVersion 19
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "net.skyscanner.backpack.BpkTestRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                groupId 'net.skyscanner.backpack'
                artifactId 'Backpack'

                name 'Backpack'
                description 'The Skyscanner Design System, Backpack, for Android'
                url 'https://github.com/Skyscanner/backpack-android'

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'Backpack Squad'
                    }
                }
                scm {
                    connection 'https://github.com/Skyscanner/backpack-android.git'
                    developerConnection 'https://github.com/Skyscanner/backpack-android.git'
                    url 'https://backpack.github.io'

                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    excludes = ['**/*.kt']
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    configurations = ['archives']
    pkg {
        repo = 'Backpack'
        name = 'Backpack'
        userOrg = 'skyscanner'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/Skyscanner/backpack-android'
        labels = ['android', 'skyscanner', 'designsystem']
        version {
            name = version
            released = new Date()
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:27.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
}

