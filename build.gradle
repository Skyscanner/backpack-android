// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
  ext.kotlin_version = '1.3.20'
  ext.dokka_version = '0.9.17'
  repositories {
    google()
    jcenter()
    maven { url 'https://jitpack.io' }
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:3.2.1'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:$dokka_version"
    classpath 'com.github.tiagoengel:screenshot-tests-for-android:fix-reference-dir-SNAPSHOT'
  }
}

allprojects {
  repositories {
    google()
    jcenter()
  }
}

subprojects {
  afterEvaluate { project ->
    task ktlint(type: JavaExec, group: "verification") {
      description = "Check Kotlin code style."
      classpath = configurations.lintClassPath
      main = "com.pinterest.ktlint.Main"

      if (project.hasProperty("files")) {
        args files.split(" ")
      } else {
        args "src/**/*.kt"
      }
      // to generate report in checkstyle format prepend following args:
      // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
      // see https://github.com/shyiko/ktlint#usage for more
    }

    task ktlintFormat(type: JavaExec, group: "formatting") {
      description = "Fix Kotlin code style deviations."
      classpath = configurations.lintClassPath
      main = "com.pinterest.ktlint.Main"
      args "-F", "src/**/*.kt"
    }

    check.dependsOn ktlint
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

def BpkVersion = '22.1.0'

ext {
  version = "$BpkVersion"
  compileSdkVersion = 29
  targetSdkVersion = 29
  minSdkVersion = 21
  buildToolsVersion = "29.0.2"
  supportLibVersion = "27.1.1"
  googlePlayServicesVersion = "15.0.1"
  androidMapsUtilsVersion = "0.5+"
  androidxAppCompat = "1.1.0"
  androidxAnnotations = "1.1.0"
  androidXLegacy = "1.0.0"
  androidMaterial = "1.0.0"
  cardView = "1.0.0"
  constraintLayout = "1.1.3"
  threeTenAbp = "1.2.1"
}
