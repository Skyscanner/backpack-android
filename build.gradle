/**
 * Backpack for Android - Skyscanner's Design System
 *
 * Copyright 2018 Skyscanner Ltd
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
  ext.kotlin_version = '1.6.10'
  ext.compose_version = '1.1.1'
  ext.dokka = "org.jetbrains.dokka:dokka-gradle-plugin:$kotlin_version"
  repositories {
    google()
    mavenCentral()
    maven { url 'https://plugins.gradle.org/m2/' }
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:7.2.1' // keep in sync with lint version - If the Gradle plugin version is X.Y.Z, then the lint version is (X+23).Y.Z.
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath dokka
    classpath 'com.facebook.testing.screenshot:plugin:0.15.0'
    classpath "org.jlleitschuh.gradle:ktlint-gradle:10.3.0"
    classpath 'io.github.gradle-nexus:publish-plugin:1.1.0'
  }
}

apply plugin: 'io.github.gradle-nexus.publish-plugin'
apply from: 'publish-root.gradle'
apply plugin: 'org.jetbrains.dokka'

allprojects {
  repositories {
    google()
    mavenCentral()
  }
}

subprojects {
  apply plugin: "org.jlleitschuh.gradle.ktlint"

  repositories {
    google()
    mavenCentral()
  }

  ktlint {
    version = "0.43.2"
    enableExperimentalRules = false
    reporters {
      reporter "checkstyle"
    }
  }

  afterEvaluate { project ->

    // for backward compatibility, will be removed in another task
    task ktlint(type: SourceTask, group: "verification") {
      description = "Check Kotlin code style."
      dependsOn(ktlintCheck)
    }

    check.dependsOn("ktlint")
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

ext {
  group = "net.skyscanner.backpack"
  dokkaOutputDir = "dokka"

  androidxAppCompat = "androidx.appcompat:appcompat:1.4.1"
  androidxAnnotations = "androidx.annotation:annotation:1.3.0"
  androidXLifecycleKtx = "androidx.lifecycle:lifecycle-runtime-ktx:2.4.1"
  androidMaterial = "com.google.android.material:material:1.6.0"
  cardView = "androidx.cardview:cardview:1.0.0"
  googleMaps = "com.google.android.gms:play-services-maps:18.0.2"
  constraintLayout = "androidx.constraintlayout:constraintlayout:2.1.3"
  swiperefreshLayout = "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
  threeTenAbpVersion = "1.4.0"
  threeTenAbp = "org.threeten:threetenbp:$threeTenAbpVersion"
  threeTenAbpAndroid = "com.jakewharton.threetenabp:threetenabp:$threeTenAbpVersion"
  picasso = "com.squareup.picasso:picasso:2.71828"
  ktx = "androidx.core:core-ktx:1.7.0"
  junit = "junit:junit:4.13.2"
  stetho = "com.facebook.stetho:stetho:1.6.0"
  junitAndroid = "androidx.test.ext:junit:1.1.3"
  espressoCore = "androidx.test.espresso:espresso-core:3.4.0"
  espressoContrib = "com.android.support.test.espresso:espresso-contrib:3.1.0"
  testRules = "androidx.test:rules:1.4.0"
  mockitoKotlin = "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
  mockitoAndroid = "org.mockito:mockito-android:4.5.1"

  coroutinesVersion = "1.6.1"
  coroutines = "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
  coroutinesTest = "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion"

  lintVersion = "30.2.0"
  lint = "com.android.tools.lint:lint:$lintVersion"
  lintApi = "com.android.tools.lint:lint-api:$lintVersion"
  lintTests = "com.android.tools.lint:lint-tests:$lintVersion"

  composeUi = "androidx.compose.ui:ui:$compose_version"
  composeMaterial = "androidx.compose.material:material:$compose_version"
  composeUiToolingPreview = "androidx.compose.ui:ui-tooling-preview:$compose_version"
  composeTestJUnit = "androidx.compose.ui:ui-test-junit4:$compose_version"
  composeUiTooling = "androidx.compose.ui:ui-tooling:$compose_version"
  composeActivity = "androidx.activity:activity-compose:1.4.0"

  accompanistFlowLayout = "com.google.accompanist:accompanist-flowlayout:0.23.1"
}

tasks.dokkaHtmlMultiModule.configure {
  outputDirectory = new File(rootProject.rootDir, "$rootProject.ext.dokkaOutputDir")
  doLast {
    createNewFile(rootProject.rootDir, "$rootProject.ext.dokkaOutputDir/404.html").text = """<!DOCTYPE html>
<html>
  <meta http-equiv="refresh" content="1;url=/android" />
</html>
"""
  }
}

static File createNewFile(File parent, String path) {
  def file = new File(parent, path)
  file.parentFile.mkdirs()
  file.createNewFile()
  return file
}
