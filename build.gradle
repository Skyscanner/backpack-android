/**
 * Backpack for Android - Skyscanner's Design System
 *
 * Copyright 2018 Skyscanner Ltd
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
  ext.kotlin_version = '1.7.20'
  ext.compose_compiler_version = '1.3.2'
  ext.dokka = "org.jetbrains.dokka:dokka-gradle-plugin:$kotlin_version"
  repositories {
    google()
    mavenCentral()
    maven { url 'https://plugins.gradle.org/m2/' }
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:7.3.1' // keep in sync with lint version - If the Gradle plugin version is X.Y.Z, then the lint version is (X+23).Y.Z.
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath dokka
    classpath 'com.facebook.testing.screenshot:plugin:0.15.0'
    classpath "org.jlleitschuh.gradle:ktlint-gradle:11.0.0"
    classpath 'io.github.gradle-nexus:publish-plugin:1.1.0'
  }
}

apply plugin: 'io.github.gradle-nexus.publish-plugin'
apply from: 'publish-root.gradle'
apply plugin: 'org.jetbrains.dokka'

allprojects {
  repositories {
    google()
    mavenCentral()
  }
}

subprojects {
  apply plugin: "org.jlleitschuh.gradle.ktlint"

  repositories {
    google()
    mavenCentral()
  }

  ktlint {
    version = "0.43.2"
    enableExperimentalRules = false
    reporters {
      reporter "checkstyle"
    }
  }

  afterEvaluate { project ->

    // for backward compatibility, will be removed in another task
    task ktlint(type: SourceTask, group: "verification") {
      description = "Check Kotlin code style."
      dependsOn(ktlintCheck)
    }

    check.dependsOn("ktlint")
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

ext {
  group = "net.skyscanner.backpack"
  dokkaOutputDir = "dokka"

  androidxAppCompat = "androidx.appcompat:appcompat:1.5.1"
  androidxAnnotations = "androidx.annotation:annotation:1.5.0"

  lifecycleVersion = "2.5.1"
  androidXLifecycleKtx = "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycleVersion"
  // The below dependencies can be removed once the following issue is fixed: https://issuetracker.google.com/issues/238425626
  androidXLifecycleViewmodel = "androidx.lifecycle:lifecycle-viewmodel:$lifecycleVersion"
  androidXLifecycleViewmodelKtx = "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycleVersion"

  androidMaterial = "com.google.android.material:material:1.7.0"
  cardView = "androidx.cardview:cardview:1.0.0"
  googleMaps = "com.google.android.gms:play-services-maps:18.1.0"
  constraintLayout = "androidx.constraintlayout:constraintlayout:2.1.4"
  swiperefreshLayout = "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
  threeTenAbpVersion = "1.4.3"
  threeTenAbp = "org.threeten:threetenbp:$threeTenAbpVersion"
  threeTenAbpAndroid = "com.jakewharton.threetenabp:threetenabp:$threeTenAbpVersion"
  picasso = "com.squareup.picasso:picasso:2.71828"
  ktx = "androidx.core:core-ktx:1.9.0"
  testCoreKtx = "androidx.test:core-ktx:1.5.0"
  kotlinReflection = "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
  junit = "junit:junit:4.13.2"
  junitKtx = 'androidx.test.ext:junit-ktx:1.1.4'
  stetho = "com.facebook.stetho:stetho:1.6.0"
  junitAndroid = "androidx.test.ext:junit:1.1.3"
  espressoCore = "androidx.test.espresso:espresso-core:3.4.0"
  espressoContrib = "com.android.support.test.espresso:espresso-contrib:3.1.0"
  testRules = "androidx.test:rules:1.5.0"
  mockitoKotlin = "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
  mockitoAndroid = "org.mockito:mockito-android:4.8.1"

  coroutinesVersion = "1.6.4"
  coroutines = "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
  coroutinesTest = "org.jetbrains.kotlinx:kotlinx-coroutines-test:$coroutinesVersion"

  lintVersion = "30.3.1"
  lint = "com.android.tools.lint:lint:$lintVersion"
  lintApi = "com.android.tools.lint:lint-api:$lintVersion"
  lintTests = "com.android.tools.lint:lint-tests:$lintVersion"

  composeVersion = "1.3.0"
  composeUi = "androidx.compose.ui:ui:$composeVersion"
  composeRuntime = "androidx.compose.runtime:runtime:$composeVersion"
  composeMaterial = "androidx.compose.material:material:$composeVersion"
  composeUiToolingPreview = "androidx.compose.ui:ui-tooling-preview:$composeVersion"
  composeTestJUnit = "androidx.compose.ui:ui-test-junit4:$composeVersion"
  composeUiTooling = "androidx.compose.ui:ui-tooling:$composeVersion"
  composeActivity = "androidx.activity:activity-compose:1.6.1"
  composeUtil = "androidx.compose.ui:ui-util:$composeVersion"
  composeFoundationV3 = "androidx.compose.foundation:foundation:1.3.0"

  accompanistFlowLayout = "com.google.accompanist:accompanist-flowlayout:0.27.0"
}

tasks.dokkaHtmlMultiModule.configure {
  outputDirectory = new File(rootProject.rootDir, "$rootProject.ext.dokkaOutputDir")
  doLast {
    createNewFile(rootProject.rootDir, "$rootProject.ext.dokkaOutputDir/404.html").text = """<!DOCTYPE html>
<html>
  <meta http-equiv="refresh" content="1;url=/android" />
</html>
"""
  }
}

static File createNewFile(File parent, String path) {
  def file = new File(parent, path)
  file.parentFile.mkdirs()
  file.createNewFile()
  return file
}
