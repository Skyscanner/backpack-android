/**
 * Backpack for Android - Skyscanner's Design System
 *
 * Copyright 2018 - 2025 Skyscanner Ltd
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
        maven { url 'https://plugins.gradle.org/m2/' }
    }
    dependencies {
        classpath libs.plugin.android
        classpath libs.plugin.kotlin
        classpath libs.plugin.detekt
        classpath libs.plugin.ksp
        classpath libs.plugin.roborazzi
    }
}

plugins {
    alias(libs.plugins.compose.compiler) apply false
}

apply from: 'publish-root.gradle'

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

subprojects {
    apply plugin: "io.gitlab.arturbosch.detekt"

    repositories {
        google()
        mavenCentral()
    }

    detekt {
        config = files("$rootDir/.detekt.yml", "$rootDir/.detekt-compose.yml")
        buildUponDefaultConfig = true
        source = files("src", "$rootDir/buildSrc/src")
    }

    dependencies {
        detektPlugins libs.detektRules.compose
        detektPlugins libs.detektRules.formatting
        detektPlugins libs.detektRules.libraries
    }
}


ext {
    group = "net.skyscanner.backpack"
}

task installGitHooks(type: Copy) {
    from new File(rootProject.rootDir, 'hooks/pre-commit')
    into { new File(rootProject.rootDir, '.git/hooks') }
    fileMode 0775
}

task installAiLabels(type: Exec) {
    description = 'Install ai-labels for AI tool detection in commits'
    group = 'setup'

    workingDir rootProject.rootDir
    commandLine 'bash', 'hooks/install-ai-labels.sh'
    ignoreExitValue = true
    standardOutput = new ByteArrayOutputStream()
    errorOutput = new ByteArrayOutputStream()

    doLast {
        def stdout = standardOutput.toString().trim()
        def stderr = errorOutput.toString().trim()
        if (stdout) println stdout
        if (stderr) System.err.println stderr
    }
}

tasks.getByPath(':app:preBuild').dependsOn installGitHooks, installAiLabels
