import com.android.build.api.dsl.ManagedVirtualDevice
import net.skyscanner.backpack.screenshots.ScreenshotTestsServer

android {
  productFlavors {
    screenshots {
      dimension "version"
      versionNameSuffix "-screenshots"
      testInstrumentationRunnerArgument "notClass", "net.skyscanner.backpack.*"
      testInstrumentationRunnerArgument "class", "net.skyscanner.backpack.docs.GenerateScreenshots"
    }
  }
  testOptions {
    animationsDisabled = true
    managedDevices {
      managedDevices.devices {
        Pixel (ManagedVirtualDevice) {
          device = "Pixel"
          apiLevel = 30
          systemImageSource = "aosp"
        }
      }
    }
  }
}

def server = new ScreenshotTestsServer(rootProject.file("docs"))

task copyInternalFontsToScreenshots(type: Copy) {
  from 'src/internal/res/font'
  from 'src/sreenshot/res/font'
}

task startScreenshotsServer() {
  doFirst {
    server.start()
  }
  finalizedBy("stopScreenshotsServer")
}

task stopScreenshotsServer() {
  doLast {
    server.close()
  }
}

task recordScreenshots() {
  dependsOn(
    "copyInternalFontsToScreenshots",
    "startScreenshotsServer",
    "PixelScreenshotsDebugAndroidTest",
  )
  finalizedBy("stopScreenshotsServer")
}
