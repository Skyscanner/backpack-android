/**
 * Backpack for Android - Skyscanner's Design System
 *
 * Copyright 2018-2021 Skyscanner Ltd
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.facebook.testing.screenshot'

def properties = new Properties()
properties.putAll(System.getenv())
if (rootProject.file("local.properties").exists()) {
  properties.load(rootProject.file("local.properties").newDataInputStream())
}

android {
  compileSdkVersion rootProject.ext.compileSdkVersion

  defaultConfig {
    applicationId 'net.skyscanner.backpack'
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.targetSdkVersion
    versionCode 1
    versionName rootProject.ext.version

    testInstrumentationRunner "net.skyscanner.backpack.BpkTestRunner"
    testInstrumentationRunnerArgument "notClass", "net.skyscanner.backpack.docs.GenerateScreenshots"

    manifestPlaceholders = [ MAPS_API_KEY : properties.getProperty("MAPS_API_KEY", "") ]
  }

  signingConfigs {
    release {
      storeFile rootProject.file('backpack.android.demo.key')
      storePassword properties.getProperty('KEYSTORE_PASSWORD')
      keyAlias properties.getProperty('KEYSTORE_ALIAS')
      keyPassword properties.getProperty('KEYSTORE_ALIAS_PASSWORD')
    }
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release
    }
  }
  flavorDimensions "version"
  productFlavors {
    oss {
      dimension "version"
      versionNameSuffix "-oss"
    }
    internal {
      dimension "version"
      versionNameSuffix "-internal"
    }
  }
  lintOptions {
    baseline file("lint-baseline.xml")
    lintConfig file("../lint.xml")
    checkReleaseBuilds false // we're already running lint separately
    warningsAsErrors true
  }
  testOptions {
    animationsDisabled = true
  }
  flavorDimensions "version"
  productFlavors {
    oss {
      dimension "version"
    }
    internal {
      dimension "version"
    }
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  kotlinOptions {
    jvmTarget = JavaVersion.VERSION_1_8.toString()
    freeCompilerArgs += [
      "-Xopt-in=kotlin.RequiresOptIn",
    ]
  }
  packagingOptions {
    exclude "**/attach_hotspot_windows.dll"
    exclude "META-INF/licenses/**"
    exclude "META-INF/AL2.0"
    exclude "META-INF/LGPL2.1"
  }
}

dependencies {
  implementation fileTree(include: ['*.jar'], dir: 'libs')
  implementation rootProject.ext.picasso
  testImplementation rootProject.ext.junit
  implementation rootProject.ext.stetho
  androidTestImplementation rootProject.ext.junitAndroid
  androidTestImplementation rootProject.ext.espressoCore
  androidTestImplementation rootProject.ext.espressoContrib
  androidTestImplementation rootProject.ext.testRules
  androidTestImplementation rootProject.ext.coroutinesTest
  implementation project(':Backpack')
}
screenshots {
  multipleDevices false
  def variant = project.gradle.startParameter.projectProperties.get("android.testInstrumentationRunnerArguments.variant")
  if (variant == null || variant.isEmpty()) {
    variant = "default"
  }
  recordDir = "screenshots/$variant"
}
